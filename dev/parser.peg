{
//from dev folder run $pegjs --cache parser.peg
var symbolTable = {
	"+":"addition",
    "*":"multiplication",
    "-":"subtraction",
    "/":"division",
    "<":"lessThan",
    ">":"greaterThan",
    "==":"equal",
    "&&":"and",
    "||":"or"
}

function createString(str){
	return {
        props:{
            jsPrimitive:{type:"string", value:str}
        }
	}
}
function createBinOp(left, right, op){
	return {
        props:{
            jsPrimitive:{type:"apply"},
            op1:left,
            op2:right,
            function:symbolTable[op]
        }
    } 
}
function createApply(arg, op){
	return {
        props:{
            jsPrimitive:{type:"apply"},
            op1:arg,
            function:op
        }
    }
}
function createFunction(name){
	return {
        props:{
        	name:createString(name),
        	jsPrimitive:{type:"function"}
        }
    }
}
function createArray(elements){
    return {
        props:{
            instanceOf:"array",
            jsPrimitive:{ type:"array", value:elements}
        }
    }
}
}
start
  = Module
  
Module 
	= declarations:(Declaration "\n"*)+ {
    	var objects = {}
        declarations.forEach(function(declaration){
            var object = declaration[0].object
            var id = declaration[0].id
        	objects[id] = object
        })
        
        return objects
    }
Declaration
	= "\n"*id:Name _ "="_ object:Object {return {object:object, id:id}}
    / BlockComment

Object "object"
	= value:New "{{"?attributes:("\n""    "*Attribute)*"}}"? {
    	var props = {}
    	attributes.forEach(function(attr){props[attr[2].name] = attr[2].value})
        props.instanceOf = props.hasOwnProperty('instanceOf') ? props.instanceOf : value
        var type = props.hasOwnProperty('type') ? props.type : value //override type property 
        //--get rid of this when switching type system
    	return {
            props:props
        } 
    }

Attribute 
    = name:Name":"" "?value:(Expression) _ Comment? {
        return {
        	name:name,
            value:value
       }
   }
Expression "expression"
	= Ternary /  Or / Array
    
Value "value"
	=  Primitive /Bool/ Number / String / Get / Object / Name

Apply "function application"
	= name:Name "("arg:Expression")"{return createApply(arg, name)}

Or "or"
	= left:And _ op:"||" _ right:Or { return createBinOp(left, right, op)}
    / And
And "and"
	= left:Equal _ op:"&&" _ right:And { return createBinOp(left, right, op)}
    /Equal
Equal "equality"
	=  left:Sum _ op:("<"/">"/"==") _ right:Equal { return createBinOp(left, right, op)}
    /Sum
Sum "sum"
	= left:Product _ op:("+"/"-") _ right:Sum { return createBinOp(left, right, op)}
    /Product
Product "product"
	= left:Value _ op:("*"/"/") _ right:Product { return createBinOp(left, right, op)}
    / Not
    
Not "not"
    ="!"value:Not {return createApply(value, "not")}
    / Apply / Value

Name "name"
	= name:[a-zA-Z0-9]+ {return name.join("")}
    
New = "new "name:Name _ Comment? {return name}
Search = query:Name {
	return {
        props:{
            jsPrimitive:{type:"search", "query":query}
        }
        }
    }
////////references
Get "get"
    = root:Search? attributes:("."Name)+ {
    function buildPath(rootObject, attr){
      var getData = {
          props:{
              jsPrimitive:{type:"get"},
              attribute:attr[1]
          }
      }
      if (rootObject !== null){
      	  getData.props.rootObject = rootObject
      }
      return getData
    }
    return attributes.reduce(buildPath, root)
}

Ternary "ternary"
    = "if" _ condition:Value _ "then"_ then:Value _"else"_ alt:Value {
    return {
        props:{
        	instanceOf:"ternary",
        	condition:condition,
        	then:then,
        	alt:alt
        }
    }
    }
     
////////primitives
Number 
	= value:Float / value:Int {
        return {
            props:{
            	jsPrimitive:{type:"number", value:value}
            }
        }
    }
    
Int "int"
	= digits:[0-9]+ {
    	return parseInt(digits.join(""))
     }
Float "number"
  = left:[0-9]+ "." right:[0-9]+ {
    return parseFloat(left.join("") + "." +   right.join("")); 
    }

String "string"
     = '"'characters:[a-zA-Z0-9]*'"' {
         var value = characters.join("")
         return createString(value)
     }

ArrayElement "array element"
	= Object / Expression
    
Array "array"
    ='['_ head:ArrayElement tail:(','_ ArrayElement)*']' {
    	var remaining = tail.map(function(expr){return expr[2]})
    	return createArray([head].concat(remaining))
    }

Bool "bool"
	= value:("true" / "false") {
    return {
        "props":{
        	jsPrimitive:{type:"bool", value:value==="true"}
        }
       }
    }
Primitive "static primitive"
    ="{"type:Name"}" {return {type:type}}
//whitespace
_ = " "*

//comment
Comment "comment"
	="//"[^\n]*

BlockComment "block comment"
    ="/*" (!"*/" .)* "*/"