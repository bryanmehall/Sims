{
var symbolTable = {
	"+":"addition",
    "*":"multiplication",
    "-":"subtraction",
    "/":"division",
    "<":"lessThan",
    ">":"greaterThan",
    "==":"equal",
    "&&":"and",
    "||":"or"
}

function createString(str){
	return {
        type:"string",
        props:{
            jsPrimitive:{type:"string", value:str}
        }
	}
}
function createBinOp(left, right, op){
	return {
    	type:"apply",
        props:{
            jsPrimitive:{type:"apply"},
            name:createString("apply"),
            op1:left,
            op2:right,
            function:symbolTable[op]
        }
    } 
}
function createApply(arg, op){
	return {
    	type:"apply",
        props:{
            jsPrimitive:{type:"apply"},
            op1:arg,
            function:op
        }
    }
}
function createFunction(name){
	return {
    	type:"function",
        props:{
        	name:createString(name),
        	jsPrimitive:{type:"function"}
        }
    }
}
}
start
  = Module
  
Module 
	= declarations:(Declaration "\n"*)+ {
    	var objects = {}
        declarations.forEach(function(declaration){
        	objects[declaration[0].id]  = declaration[0]
        })
        
        return objects
    }
Declaration
	= "\n"*id:Name _ "="_ object:Object {return Object.assign({}, object, {id:id})}
    / BlockComment

Object "object"
	= value:New "{{"?attributes:("\n""    "*Attribute)*"}}"? {
    	var props = {}
    	attributes.forEach(function(attr){props[attr[2].name] = attr[2].value})
        props.instanceOf = props.hasOwnProperty('instanceOf') ? props.instanceOf : value
        var type = props.hasOwnProperty('type') ? props.type : value //override type property 
        //--get rid of this when switching type system
    	return {
        	type:type,
            props:props
        } 
    }

Attribute 
    = name:Name":"" "?value:(Expression) _ Comment? {
        return {
        	name:name,
            value:value
       }
   }
Expression "expression"
	= Ternary /  Or
    
Value "value"
	=  Primitive /Bool/ Number / String / Get / Object / Name

Apply "function application"
	= name:Name "("arg:Expression")"{return createApply(arg, name)}

Or "or"
	= left:And _ op:"||" _ right:Or { return createBinOp(left, right, op)}
    / And
And "and"
	= left:Equal _ op:"&&" _ right:And { return createBinOp(left, right, op)}
    /Equal
Equal "equality"
	=  left:Sum _ op:("<"/">"/"==") _ right:Equal { return createBinOp(left, right, op)}
    /Sum
Sum "sum"
	= left:Product _ op:("+"/"-") _ right:Sum { return createBinOp(left, right, op)}
    /Product
Product "product"
	= left:Value _ op:("*"/"/") _ right:Product { return createBinOp(left, right, op)}
    / Apply / Value

Name "name"
	= name:[a-zA-Z0-9]+ {return name.join("")}
    
New = "new "name:Name _ Comment? {return name}
Search = query:Name {
	return {
		type:"search",
        props:{
            jsPrimitive:{type:"search", "query":query}
        }
        }
    }
////////references
Get "get"
    = root:Search? attributes:("."Name)+ {
    function buildPath(rootObject, attr){
      var getData = {
          type:"get",
          props:{
              jsPrimitive:{type:"get"},
              attribute:attr[1]
          }
      }
      if (rootObject !== null){
      	  getData.props.rootObject = rootObject
      }
      return getData
    }
    return attributes.reduce(buildPath, root)
}

Ternary "ternary"
    = "if" _ condition:Value _ "then"_ then:Value _"else"_ alt:Value {
    return {
        type:"ternary",
        props:{
        	instanceOf:"ternary",
        	condition:condition,
        	then:then,
        	alt:alt
        }
    }
    }
     
////////primitives
Number 
	= value:Float / value:Int {
        return {
         	type:"number",
            props:{
            	jsPrimitive:{type:"number", value:value}
            }
        }
    }
    
Int "int"
	= digits:[0-9]+ {
    	return parseInt(digits.join(""))
     }
Float "number"
  = left:[0-9]+ "." right:[0-9]+ {
    return parseFloat(left.join("") + "." +   right.join("")); 
    }

String "string"
     = '"'characters:[a-zA-Z]*'"' {
         var value = characters.join("")
         return createString(value)
     }
Bool "bool"
	= value:("true" / "false") {
    return {
    	type:"bool",
        "props":{
        	jsPrimitive:{type:"bool", value:value==="true"}
        }
       }
    }
Primitive "static primitive"
    ="{"type:Name"}" {return {type:type}}
//whitespace
_ = " "*

//comment
Comment "comment"
	="//"[^\n]*

BlockComment "block comment"
    ="/*" (!"*/" .)* "*/"